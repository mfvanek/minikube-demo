plugins {
  id 'java'
  id 'org.springframework.boot' version '3.0.6'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'com.bmuschko.docker-java-application' version '9.3.1'
  id 'com.google.osdetector' version '1.7.3'
}

group = 'io.github.mfvanek'
version = '0.1.6'

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'io.micrometer:micrometer-registry-prometheus:1.11.0'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
  testImplementation 'org.assertj:assertj-core:3.24.2'

  // TODO spring-context-indexer on Spring Boot 3 works unsatisfactory. Needs more investigation
  //annotationProcessor 'org.springframework:spring-context-indexer'
  //testAnnotationProcessor 'org.springframework:spring-context-indexer'

  // https://github.com/netty/netty/issues/11020
  if (osdetector.arch == "aarch_64") {
    testImplementation 'io.netty:netty-all:4.1.92.Final'
  }
}

test {
  useJUnitPlatform()
}

springBoot {
  buildInfo()
}

docker {
  javaApplication {
    //baseImage = 'eclipse-temurin:11.0.19_7-jre-focal'
    baseImage = 'eclipse-temurin:17.0.7_7-jre-focal'
    //baseImage = 'eclipse-temurin:20.0.1_9-jre'
    maintainer = 'Ivan Vakhrushev'
    images = ["${project.group}/${project.name}:${project.version}", "${project.group}/${project.name}:latest"]
  }
}
